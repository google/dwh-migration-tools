/*
 * Copyright 2022-2025 Google LLC
 * Copyright 2013-2021 CompilerWorks
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
import java.nio.file.Files
import java.util.stream.Collectors

plugins {
    id 'dwh-migration-dumper.java-library-conventions'
    alias libs.plugins.shadow
}

configurations {
    configureEach {
        //We use logback, so log4j implementation must be excluded
        exclude group: 'log4j', module: 'log4j'
        exclude group: 'org.eclipse.jetty'
        exclude group: 'co.cask.tephra'   //We don't use HBase

        // *-over-slf4j bridges used
        exclude group: "ch.qos.reload4j", module: "reload4j"
        exclude group: "org.slf4j", module: "slf4j-reload4j"
        exclude group: "org.bouncycastle", module: "bcprov-jdk15on" // We use 18, which is the latest and support JDK 1.8+
        exclude group: "javax.servlet.jsp", module: "jsp-api"
    }
}

sourceSets {
    hive312 // build a runtime to run Hive 3.1.2 metastore/server
}

dependencies {
    api libs.libthrift

    implementation libs.guava
    implementation libs.jackson.databind

    compileOnly libs.javax.annotation.api

    hive312RuntimeOnly libs.apache.avro
    hive312RuntimeOnly libs.hive.common
    hive312RuntimeOnly libs.hive.exec
    hive312RuntimeOnly libs.hive.service
    hive312RuntimeOnly libs.hive.metastore
    hive312RuntimeOnly libs.hive.jdbc
    hive312RuntimeOnly libs.logback.classic
    hive312RuntimeOnly libs.jcl.over.slf4j
    hive312RuntimeOnly libs.hadoop.common

    //Overrides for hadoop-common transient deps
    hive312RuntimeOnly libs.netty.handler // explicit version for vulnerabilities update
    hive312RuntimeOnly libs.dnsjava // explicit version for vulnerabilities update
    hive312RuntimeOnly libs.nimbus.jose.jwt // explicit version for vulnerabilities update
    hive312RuntimeOnly libs.zookeeper // explicit version for vulnerabilities update
    hive312RuntimeOnly libs.common.configuration2 // explicit version for vulnerabilities update
    hive312RuntimeOnly libs.bouncycastle // explicit version for vulnerabilities update

    // For HiveServer2
    hive312RuntimeOnly libs.tez.common
    hive312RuntimeOnly libs.tez.dag

    // Starting from version 1.8 the binding mechanics changed
    hive312RuntimeOnly("org.slf4j:slf4j-api") {
        version {
            strictly '1.7.13'
        }
    }
    // Hive has many dependencies on core through LogDivertAppender, use latest version
    hive312RuntimeOnly("org.apache.logging.log4j:log4j-core") {
        version {
            strictly '2.18.0'
        }
    }
    hive312RuntimeOnly("com.google.protobuf:protobuf-java") {
        version {
            strictly '3.25.5'
        }
    }
    hive312RuntimeOnly("com.google.guava:guava") {
        version {
            strictly '32.1.2-jre'
        }
    }
}

dependencyLocking {
    lockAllConfigurations()
}

shadowJar {
    zip64 true
    configurations = [project.configurations.hive312RuntimeClasspath]

    exclude 'META-INF/org/apache/logging/log4j/core/config/plugins/Log4j2Plugins.dat' // or write a custom merger

    dependencies {
        exclude(dependency('org.slf4j:slf4j-log4j12'))
        exclude(dependency('log4j:log4j'))
        exclude(dependency('org.apache.logging.log4j:log4j-slf4j-impl'))
        exclude(dependency('org.datanucleus:.*')) // can't be safely shadowed
    }

    exclude 'META-INF/versions/**' // Exclude the specific Java 8+ deps versions

    relocate 'org.apache.thrift', 'shadow.org.apache.thrift' // this is the whole point of doing this
    relocate 'com.google.common', 'shadow.com.google.common'
    relocate 'org.junit', 'shadow.org.junit'
    relocate 'com.facebook', 'shadow.com.facebook'
    relocate 'org.apache.commons.lang3', 'shadow.org.apache.commons.lang3'
    relocate 'com.fasterxml', 'shadow.com.fasterxml'

}

tasks.register("generateThrift") {
    def thriftFiles = Files.list(projectDir.toPath().resolve('src/main/thrift'))
        .filter { it.toString().endsWith('.thrift') }
        .collect(Collectors.toList())
    def outputPath = project.projectDir.toPath().resolve("src/main/gen-java")
    inputs.files(thriftFiles)
    outputs.dir(outputPath)

    doFirst {
        project.delete(outputPath)
        thriftFiles.each { thriftPath ->
            project.exec {
                workingDir project.projectDir.toPath().resolve('src/main')
                commandLine 'thrift', '-r', '--gen', 'java', thriftPath
            }
        }
    }
}

sourceSets.main.java {
    srcDir file('src/main/gen-java')
}
