/*
 * Copyright 2022-2023 Google LLC
 * Copyright 2013-2021 CompilerWorks
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    dependencies {
        classpath 'com.github.jk1:gradle-license-report:1.17'
    }
}

plugins {
    id 'dwh-migration-dumper.java-application-conventions'
}

apply plugin: 'com.github.jk1.dependency-license-report'

configurations {
    sources {
        transitive = true
    }
}

def mockitoVersion = '4.11.0'
def autoValueVersion = '1.10.2'

dependencies {
    compileOnly "com.google.auto.value:auto-value-annotations:${autoValueVersion}"

    annotationProcessor "com.google.auto.value:auto-value:${autoValueVersion}"

    implementation project(':dumper:lib-common')
    implementation project(':dumper:lib-dumper-spi')
    implementation project(':dumper:lib-ext-bigquery')
    implementation project(':dumper:lib-ext-hive-metastore')

    implementation "com.google.guava:guava"
    implementation "net.sf.jopt-simple:jopt-simple"
    implementation "org.anarres.jdiagnostics:jdiagnostics"
    implementation "commons-io:commons-io"
    implementation "org.apache.commons:commons-csv"
    implementation "org.apache.commons:commons-lang3"
    implementation "org.springframework:spring-core"
    implementation "org.springframework:spring-jdbc"
    implementation "com.zaxxer:HikariCP"
    implementation "com.swrve:rate-limited-logger"
    implementation "com.fasterxml.jackson.core:jackson-databind"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml"
    implementation "com.google.cloud:google-cloud-bigquery"
    implementation "com.google.cloud:google-cloud-nio:0.127.1"
    implementation "com.google.cloud:google-cloud-kms:2.23.0"
    implementation "org.apache.httpcomponents.client5:httpclient5:5.2.1"

    runtimeOnly "ch.qos.logback:logback-classic"
    runtimeOnly "org.slf4j:jcl-over-slf4j"

    runtimeOnly "org.postgresql:postgresql"
    runtimeOnly "net.snowflake:snowflake-jdbc"
    runtimeOnly "com.amazon.redshift:redshift-jdbc42"
    runtimeOnly "com.amazonaws:aws-java-sdk-redshift"

    testFixturesApi testFixtures(project(':dumper:lib-common'))

    testImplementation "org.xerial:sqlite-jdbc"
    testImplementation "org.postgresql:postgresql"
    testImplementation "com.github.stefanbirkner:system-rules"
    testImplementation "joda-time:joda-time"
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"


    // Test Hive 3.1.2
    testImplementation project(path: ':dumper:lib-ext-hive-metastore', configuration: 'shadow')
    // Excluded during shadowJar step, same versions as exclusions
    testRuntimeOnly "org.datanucleus:datanucleus-api-jdo:4.2.4"
    testRuntimeOnly "org.datanucleus:datanucleus-core:4.1.17"
    testRuntimeOnly "org.datanucleus:javax.jdo:3.2.0-m3"
    testRuntimeOnly "org.datanucleus:datanucleus-rdbms:4.1.19"

    sources "org.slf4j:jcl-over-slf4j:1.7.14@sources"
    sources "ch.qos.logback:logback-classic:1.2.11@sources"
}

startScripts {
    classpath = files('$APP_HOME/lib/*')
}

application {
    // Define the main class for the application.
    mainClass = 'com.google.edwmigration.dumper.application.dumper.Main'
    applicationName = 'dwh-migration-dumper'
}

task cloudExtractorStartScripts(type: CreateStartScripts) {
    outputDir = layout.buildDirectory.dir('install/cloud-extractor/bin').get().asFile
    classpath = tasks.startScripts.classpath
    mainClass = 'com.google.edwmigration.dumper.application.dumper.clouddumper.Main'
    applicationName = 'dwh-cloud-extractor'
}

licenseReport {
    // onlyIf { ! gradle.startParameter.offline }
    filters = [
        new com.github.jk1.license.filter.LicenseBundleNormalizer(bundlePath: rootProject.file("gradle/license-bundle-normalizer.json"), createDefaultTransformationRules: true)
    ]
    renderers = [
        // new com.github.jk1.license.render.XmlReportRenderer("licenses.xml", "Licenses of Third Party Dependencies"),
        new com.github.jk1.license.render.CsvReportRenderer(),
        new com.github.jk1.license.render.InventoryHtmlReportRenderer("index.html", "Licenses of Third Party Dependencies")
    ]
    allowedLicensesFile = rootProject.file("gradle/license-allowed.json")
}

tasks.named('check') {
    dependsOn 'checkLicense'
}

tasks.register('generateSourceMirror', Copy) {
    from {
        dependencies.createArtifactResolutionQuery()
            .forComponents(
                configurations.runtimeClasspath.incoming.resolutionResult
                    .allDependencies.collect { it.selected.id }
            )
            .withArtifacts(JvmLibrary, SourcesArtifact)
            .execute()
            .resolvedComponents
            .collectMany {
                it.artifactResults
                    .collect { it.file.path }
            }
    }
    into layout.buildDirectory.dir('mirror/sources')
    outputs.dir "mirror/sources"
}

tasks.register('copyGceLauncher', Copy) {
    from 'src/main/sh/cloud_extractor/gce_launcher.sh'
    into layout.buildDirectory.dir('cloud_extractor/launcher')
    filter(ReplaceTokens, tokens: [dumper_version: project.version])
    filteringCharset = 'UTF-8'
}

distributions {
    main {
        distributionBaseName = "app"
    }
    published {
        distributionBaseName = "dwh-migration-dumper"
        contents {
            from installDist
            from(cloudExtractorStartScripts) {
                into "bin"
            }
            from(generateLicenseReport) {
                into "docs/licenses"
            }
            from(generateSourceMirror) {
                into "docs/sources"
            }
        }
    }
}
